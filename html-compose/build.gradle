plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'maven-publish'
}

def VERSION = "1.0.0"

android {
    compileSdk 32

    defaultConfig {
        minSdk 21
        targetSdk 32

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion compose_version
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    publishing {
        singleVariant('release') {
            withSourcesJar()
        }
    }
    namespace 'net.sileader.html'

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}

dependencies {
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.material:material:$compose_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_version"

    implementation 'androidx.core:core-ktx:1.8.0'
    implementation 'androidx.appcompat:appcompat:1.4.2'
    implementation 'com.google.android.material:material:1.6.1'
    implementation 'androidx.activity:activity-compose:1.4.0'
    implementation 'org.jsoup:jsoup:1.15.1'

    implementation "androidx.compose.ui:ui-tooling:$compose_version"

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.robolectric:robolectric:4.9'
    testImplementation 'androidx.test.ext:junit:1.1.4'
    testImplementation 'androidx.test.espresso:espresso-core:3.5.0'
    testImplementation "androidx.compose.ui:ui-test-junit4:$compose_version"
    testImplementation "androidx.compose.ui:ui-test-manifest:$compose_version"
    testImplementation 'androidx.test:runner:1.5.1'
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/SiLeader/html-view-compose")
            credentials {
                Properties properties = new Properties()
                properties.load(project.rootProject.file('local.properties').newDataInputStream())
                username = properties.getProperty("gpr.user", System.getenv("USERNAME"))
                password = properties.getProperty("gpr.token", System.getenv("TOKEN"))
            }
        }
    }

    publications {
        GitHubPackages(MavenPublication) {
            groupId "net.sileader"
            artifactId "html-view-compose"
            version "$VERSION"

            afterEvaluate {
                from components.release
            }

            pom {
                url = 'https://github.com/SiLeader/html-view-compose'
                licenses {
                    license {
                        name = 'Mozilla Public License 2.0'
                    }
                }
            }
        }
    }
}